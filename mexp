#!/usr/bin/env perl

use open ":std", ":encoding(UTF-8)";
use utf8;
use strict;
use warnings;
use v5.32;

use lib ".";
use GotMArc qw(san $logo initpage endpage);

my $outdir = $ENV{'OUTDIR'};
die 'Set $OUTDIR' unless defined $outdir;
mkdir $outdir;

my $tid;
while (<>) {
	chomp;

	m/^([^ ]+) <([^>]+)> (.+)(\d{4}-\d{2}-\d{2} \d{2}:\d{2}) <([^>]+)> (.*)/;
	die "can't parse: $_" unless defined $1;
	my ($fname, $mid, $indent, $date, $from, $subj) = ($1, $2, $3, $4, $5, $6);
	$subj = san($subj);
	$subj =~ s/\s+/ /g;
	$subj =~ s/\s+$//;

	$mid =~ s,_,__,g;
	$mid =~ s,/,_,g;

	my $dest = "$outdir/mail/$mid.html";
	next if -f $dest;

	my $level = length($indent) - 1;
	$level = 10 if $indent =~ m/\.\.\d{2}\.\./;

	$tid = $mid if $level == 0;
	die "unknown tid" unless defined $tid;

	open(my $fh, '>', "$dest") or die "can't open $dest: $!";

	initpage $fh, $subj;

	# prepare the parts listing file
	$ENV{'MESSAGE_ID'} = $mid;
	open(my $parts, '+>', "parts.html")
	    or die "can't create parts.html: $!";

	open(my $mshow, "-|", "mshow", "-nNA", "text/plain", $fname)
	    or die "can't exec mshow: $!";

	open(my $text, '>', "$outdir/text/$mid.txt")
	    or die "can't open $outdir/text/$mid.txt: $!";

	print $fh "<header class='mail-header'>";
	print $fh "<p>";
	print $fh $logo;
	print $fh "<a href='/'>Index</a>";
	print $fh " | ";
	print $fh "<a href='/thread/$tid.html#$mid'>Thread</a>";
	print $fh "</p>";
	print $fh "<dl>";
	while (<$mshow>) {
		chomp;
		say $text $_;
		last if /^$/;
		my ($h, $v) = m/^([-A-Za-z]+): (.*)/;
		die "bogus line? $fname : $_" unless (defined $h and defined $v);

		# drop the (1 day ago) string
		$v =~ s/\(.*\)//g if ($h eq "Date");

		print $fh "<dt>", san($h), ":</dt>";
		print $fh "<dd>", san($v), "</dd>";
	}
	print $fh "</dl>";
	print $fh "<p>Download raw <a href='/text/$mid.txt'>body</a>.</p>";
	print $fh "</header>";

	my $body = do {
		local $/ = undef;
		<$mshow>;
	};

	print $fh "<pre>";
	print $fh san($body // "");
	print $fh "</pre>";

	print $text $body;

	# generate the listing for the exported parts
	my $part_seen = 0;
	while (<$parts>) {
		if (!$part_seen) {
			$part_seen = 1;
			say $fh "<ul class='parts'>";
		}
		print $fh $_;
	}
	say $fh "</ul>" if $part_seen;

	endpage $fh;

	close($text);
	close($mshow);
	close($parts);
	close($fh);
}

unlink "parts.html";
